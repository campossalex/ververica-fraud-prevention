resource "random_string" "sg_suffix" {
  length  = 6
  upper   = false
  special = false
}

# Security group to access to all post
resource "aws_security_group" "fraud-prevention_sgn" {
  name        = "labday_sgn-${random_string.sg_suffix.result}"

  ingress {
    description     = "Allow all traffic within the same security group"
    protocol    = "-1"     # ALL
    from_port   = 0
    to_port     = 0
    self        = true
  }

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 8080
    to_port     = 8080
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 8085
    to_port     = 8085
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 9090
    to_port     = 9090
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 4200
    to_port     = 4200
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# EC2 registration form instances
resource "aws_instance" "registration_form" {
  ami           = "ami-004e960cde33f9146"
  instance_type = "t2.micro"
  key_name      = var.key_name
  vpc_security_group_ids = [aws_security_group.fraud-prevention_sgn.id]

  root_block_device {
    volume_size = 20          # GB
    volume_type = "gp3"       # General-purpose SSD (recommended)
    delete_on_termination = true
  }

  # Run as root using user_data
  user_data = <<-EOF
    #!/bin/bash
    set -xe

    # Clone the repo
    cd /root
    git clone ${var.git_repo} ververica-platform-playground

    # Run the registration form setup
    cd /root/ververica-platform-playground/registration-app
    sudo ./setup.sh > /var/log/regform_setup.log 2>&1

  EOF

  tags = {
    Name = "labday-registrationform"
    owner = var.owner
  }
}

# EC2 lab instances
resource "aws_instance" "labday" {
  count         = var.instance_count
  ami           = var.instance_ami
  instance_type = var.instance_type
  key_name      = var.key_name
  vpc_security_group_ids = [aws_security_group.fraud-prevention_sgn.id]

  root_block_device {
    volume_size = 40          # GB
    volume_type = "gp3"       # General-purpose SSD (recommended)
    delete_on_termination = true
  }

  # Run as root using user_data
  user_data = <<-EOF
    #!/bin/bash
    set -euxo pipefail

    # Install git
    yum update -y
    yum install -y git

    # Store registration form ip in each lab environment instance
    echo "${aws_instance.registration_form.private_ip}" > /root/regform-ip.txt

    # Clone the repo
    cd /root
    git clone ${var.git_repo} ververica-platform-playground

    # Run pre-install script
    sudo ./ververica-platform-playground/pre-install.sh > /var/log/fraud_prevention_setup.log 2>&1

    # Run setup script
    sudo ./ververica-platform-playground/setup.sh -e ${var.edition} > /var/log/fraud_prevention_setup.log 2>&1

    # Run post-install script   
    sudo ./ververica-platform-playground/post-install.sh > /var/log/fraud_prevention_setup.log 2>&1

    # Run response-install script   
    sudo ./ververica-platform-playground/response-install.sh > /var/log/fraud_prevention_setup.log 2>&1

  EOF

  tags = {
    Name = "fraud-prevention-instance-${count.index}"
    owner = var.owner
  }


}

resource "null_resource" "enterprise_license" {
  count = var.edition == "enterprise" ? var.instance_count : 0

  triggers = {
    instance_id = aws_instance.labday[count.index].id
  }


  provisioner "file" {
    on_failure = continue
    source      = "${path.module}/../setup/helm/values-license.yaml"
    destination = "values-license.yaml"

    connection {
      type        = "ssh"
      user        = "ec2-user"
      host        = aws_instance.labday[count.index].public_ip
      private_key = file("${path.module}/key-pair/${var.key_name}.pem")
    }
  }

  provisioner "remote-exec" {
    on_failure = continue
    inline = [
      "sudo mv /home/ec2-user/values-license.yaml /values-license.yaml",
      "sudo chown root:root /values-license.yaml",
      "sudo chmod 0644 /values-license.yaml",
    ]

    connection {
      type        = "ssh"
      user        = "ec2-user"
      host        = aws_instance.labday[count.index].public_ip
      private_key = file("${path.module}/key-pair/${var.key_name}.pem")
      timeout     = "5m"
    }
  }

  depends_on = [aws_instance.labday]

}
